import org.apache.tools.ant.filters.EscapeUnicode

plugins {
    id "java"
    id "net.ltgt.errorprone" version "1.1.1"
}

archivesBaseName = "Beezig"
version = "7.0.0"
sourceCompatibility = "1.8"
targetCompatibility = "1.8"

configurations {
    embed
    compile.extendsFrom embed
}

repositories {
    jcenter()
    mavenCentral()
    maven {
        url "https://dl.bintray.com/5zig-reborn/maven"
    }
    maven {
        url "https://libraries.minecraft.net"
    }
}

dependencies {
    errorprone("com.google.errorprone:error_prone_core:latest.release")
    errorproneJavac("com.google.errorprone:javac:9+181-r4173-1")

    embed group: 'pw.roccodev.beezig', name: 'hive-api-wrapper', version: '3.0-async-alpha'
    compile group: 'eu.5zigreborn', name: 'plugin-api', version: '3.14.1'
    compile group: 'com.mojang', name: 'authlib', version: '1.6.25'
    compile group: 'com.googlecode.json-simple', name: 'json-simple', version: '1.1.1'
}

tasks.withType(ProcessResources).each { task ->
    task.from(task.getSource()) {
        include '**/*.properties'
        filter(EscapeUnicode) // native2ascii for i18n files
    }
}

jar {
    from {
        duplicatesStrategy DuplicatesStrategy.EXCLUDE
        configurations.embed.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }

    exclude 'LICENSE.txt'
    exclude 'META-INF/*.RSA'
    exclude 'META-INF/*.SF'
}